import org.gradle.api.JavaVersion

buildscript {
    project.ext['CERN_VM'] = System.getProperty('CERN_TECHNET_VM') ?: System.getenv('CERN_TECHNET_VM') ?: project.hasProperty('CERN_TECHNET_VM') ?: false
    project.ext['DEPLOYMENT'] = System.getProperty('deployment') ?: false
    project.ext['VCS_TAG'] = System.getProperty('TRAVIS_TAG') ?: System.getenv('TRAVIS_TAG')

    project.ext['BINTRAY.userName'] = System.getProperty('BINTRAY_USER') ?: System.getenv('BINTRAY_USER')
    project.ext['BINTRAY.apiToken'] = System.getProperty('BINTRAY_API_TOKEN') ?: System.getenv('BINTRAY_API_TOKEN')

    project.ext['SONATYPE.userName'] = System.getProperty('SONATYPE_USER') ?: System.getenv('SONATYPE_USER')
    project.ext['SONATYPE.password'] = System.getProperty('SONATYPE_PASSWORD') ?: System.getenv('SONATYPE_PASSWORD')

    repositories {
        if (project['CERN_VM']) {
            maven { url 'http://artifactory.cern.ch/ds-jcenter' }
            maven { url 'http://artifactory.cern.ch/development' }
        } else {
            jcenter()
            maven { url 'https://plugins.gradle.org/m2/' }
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.jk1:gradle-license-report:1.5'
    }
}


plugins {
    id 'org.openjfx.javafxplugin' version '0.0.9' apply false
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'org.openjfx.javafxplugin'

group = project['POM.groupId']

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    if (project['CERN_VM']) {
        maven { url 'http://artifactory.cern.ch/ds-jcenter' }
        maven { url 'http://artifactory.cern.ch/development' }
    } else {
        /* For minifx components: */
        maven { url "https://dl.bintray.com/minifx/minifx-repo" }
        /* For the fontawesome icons, to be compatible with java 8 */
        maven { url "https://dl.bintray.com/jerady/maven" }
        jcenter()
        //mavenCentral()
    }
}

dependencies {
    compile group: 'org.minifx', name: 'minifx-fxml', version: '2.0.0'

    compile group: 'org.controlsfx', name: 'controlsfx', version: '11.0.3'

    compile group: 'com.google.guava', name: 'guava', version: guavaVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
    compile group: 'org.springframework', name: 'spring-core', version: springVersion
    compile group: 'org.springframework', name: 'spring-context', version: springVersion

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
    testCompile group: 'org.testfx', name: 'testfx-core', version: '4.0.15-alpha'
    testCompile group: 'org.testfx', name: 'testfx-junit', version: '4.0.15-alpha'
    testCompile group: 'org.springframework', name: 'spring-test', version: springVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.4.1'
}

sourceSets {
    main {
        resources { srcDirs = ['src/main/java'] }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled false
    }
}

javadoc { options.encoding = "UTF-8" }

if (!project.tasks.findByName("javadocJar")) {
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
}

if (!project.tasks.findByName("sourcesJar")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

if (!project['CERN_VM']) {
    println 'Applying licensing report'
    apply plugin: 'com.github.jk1.dependency-license-report'

    licenseReport {
        renderers = [this.class.classLoader.loadClass('com.github.jk1.license.render.InventoryHtmlReportRenderer').newInstance()]
        filters = [this.class.classLoader.loadClass('com.github.jk1.license.filter.LicenseBundleNormalizer').newInstance()]
    }
}

if (project['DEPLOYMENT']) {
    println 'Applying deployment scripts'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply from: 'https://raw.githubusercontent.com/ossgang/gradle-scripts/master/deployment/bintray-deploy.gradle'
}

javafx {
    // version = "12.0.1"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web']
}

task printJavaVersion {
    println 'Java Version: ' + JavaVersion.current();
}

